apply plugin: 'com.android.application'
apply plugin: 'realm-android'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 27
        versionCode 18
        versionName "0.11.2"

        buildConfigField "String", "TIME_FORMAT", '"HH:mm"'
        buildConfigField "String", "DATE_FORMAT", '"yyyy-MM-dd HH:mm:ss"'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

        }

        debug {
            debuggable true
            buildConfigField "String", "WEB_BASE_URL", '"http://10.0.2.2:8080/"'
        }
    }
    flavorDimensions "demo", "elections"
    productFlavors {
        live {
            dimension "demo"
        }
        demo {
            dimension "demo"
            applicationIdSuffix ".demo"
            versionNameSuffix "-demo"
        }


        // dimension: elections
        pl2018 {
            dimension "elections"
            applicationId "pl.org.epf.monitorujwybory"
            buildConfigField "String", "WEB_BASE_URL", '"https://mvapi.epf.org.pl/"'
            buildConfigField "String", "GUIDE_URL", '"https://github.com/epforgpl/monitorizare-vot/raw/master/forms-import/Poradnik_OiA_Short_v8%20-1810.pdf"'
            buildConfigField "String", "ORGANISATION_WEB_URL", '"http://odpowiedzialnapolityka.pl/index.php/obserwatorzy/"'
            buildConfigField "String", "SERVICE_CENTER_PHONE_NUMBER", '""'
        }
        ro2015 {
            dimension "elections"
            applicationId "ro.code4.monitorizarevot"
            buildConfigField "String", "WEB_BASE_URL", '"http://viuat.azurewebsites.net/"'
            buildConfigField "String", "GUIDE_URL", '"http://monitorizare-vot-ghid.azurewebsites.net/"'
            buildConfigField "String", "ORGANISATION_WEB_URL", '"https://code4.ro/"'
            buildConfigField "String", "SERVICE_CENTER_PHONE_NUMBER", '"0800 080 200"'
        }
//        ro2018referendum {
//            dimension "elections"
//        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    // set different content provider for each variant
    // kudos to https://stackoverflow.com/questions/51040002/declare-variable-into-build-gradle-that-match-the-applicationid-with-flavors-suf
    applicationVariants.all { variant ->
        variant.resValue "string", "authorities", applicationId + '.contentprovider'
    }
}

repositories {
    maven { url "http://dl.bintray.com/pixplicity/maven" }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'

    implementation 'com.squareup.retrofit2:retrofit:2.1.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.1.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.4.1'
    implementation 'com.pixplicity.easyprefs:library:1.9.0@aar'

    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'io.reactivex:rxjava:1.1.6'

    implementation 'org.greenrobot:eventbus:3.0.0'
    implementation 'net.hockeyapp.android:HockeySDK:4.1.1'
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" // << "-Xlint:deprecation"
    }
}
